plugins {
    id 'org.openapi.generator' version '6.6.0'
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-webflux'
    implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
    implementation 'org.springframework.boot:spring-boot-starter-data-r2dbc'
    implementation 'org.springframework.boot:spring-boot-starter-data-redis-reactive'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    
    //Redis
    implementation 'io.lettuce:lettuce-core'
    
    //OpenAPI
    implementation 'jakarta.annotation:jakarta.annotation-api:2.1.1'
    implementation 'org.openapitools:jackson-databind-nullable:0.2.6'
    implementation 'io.swagger.core.v3:swagger-annotations:2.2.15'
    
    //DB
    runtimeOnly 'io.r2dbc:r2dbc-h2'
    runtimeOnly 'com.h2database:h2'
    
    //Lombok
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    
    //Tests
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'io.projectreactor:reactor-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

openApiGenerate {
    generatorName = "java"
    inputSpec = "$rootDir/payment-api.yaml"
    outputDir = "$buildDir/generated"
    apiPackage = "com.shop.main.client.api"
    modelPackage = "com.shop.main.client.model"
    configOptions = [
        library: "webclient",
        useSpringBoot3: "true",
        interfaceOnly: "true",
        skipDefaultInterface: "true",
        useJakartaEe: "true"
    ]
}

sourceSets {
    main {
        java {
            srcDirs += "$buildDir/generated/src/main/java"
        }
    }
}

compileJava.dependsOn tasks.openApiGenerate